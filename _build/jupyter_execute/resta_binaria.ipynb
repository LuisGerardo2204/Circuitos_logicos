{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "d49574a6",
   "metadata": {},
   "source": [
    "# Resta binaria\n",
    "\n",
    "Un método muy popular para realizar una resta de dos númneros binarios es el uso del complemento a dos. El complemento a dos de un número binario se obtiene de la sigiente forma:\n",
    "\n",
    "Se determina el complemento a uno de un número binario de $N$ dígitos al aplicar la negación a cada uno de los bits de dicho número, por ejemplo, el complemento a 1 del número binario $101101$ es $010010$. De manera similar, para otras bases se define el complemento; el complemento a 9 de un número decimal se obtiene al al restar cada dígito de 9, para el caso del sistema octal, se restan de 7 y para el hexadecimal se restan de 15:\n",
    "\n",
    "a. El complemento a $9$ de $876$ es: $123$.\n",
    "\n",
    "b. El complemento a $15$ de $FE$ es: $EA$.\n",
    "\n",
    "c. El complemento a $7$ de $54$ es: $23$. \n",
    "\n",
    "Como puede observarse, determinar el complemento a 1 de un número binario es más simple. El complemento a 2 se obtiene al sumar un uno al complemento a unos de un número binario. \n",
    "\n",
    "a. El complemento a 2 de $101110$ es: $010001$.\n",
    "\n",
    "b. El complemento a 2 de $111111$ es: $000001$.\n",
    "\n",
    "Para realizar la resta binaria de dos números, se obtiene el complemento a 2 de uno de los operandos y se le suma al operando restante. Por ejemplo, sean los números binarios $X=1011011$ e $Y=1001110$, la resta $Z=X-Y$ se obtiene al sumar el complemento a 2 de $Y$ y $X$.\n",
    "\n",
    "$$\n",
    "X+comp_2(Y)=1011011+0110001=91_{10}-78_{10}=13_{10}\n",
    "$$\n",
    "\n",
    "$$\n",
    "\\begin{eqnarray}\n",
    " &1011011\\\\\n",
    "+&0110010\\\\\n",
    "&\\overline{~~~~~~~~~~~~~~~~~}\\\\\n",
    "Z=1&0001101\n",
    "\\end{eqnarray}\n",
    "$$\n",
    "Se ignora el último acarreo y solo se considera al número $1101=13_{10}$ como el resultado de la resta. Por otra parte, la resta $W=Y-X$ se calcula como: \n",
    "\n",
    "$$\n",
    "Y+comp_2(X)=1001110+0100101=78_{10}-91_{10}=-13_{10}\n",
    "$$\n",
    "\n",
    "$$\n",
    "\\begin{eqnarray}\n",
    "  &1001110\\\\\n",
    " +&0100101\\\\\n",
    "&\\overline{~~~~~~~~~~~~~~~~~}\\\\\n",
    "Z=&1110011\n",
    "\\end{eqnarray}\n",
    "$$\n",
    "\n",
    "En este caso, al no haber acarreo, se calcula el complemento a dos del resultado para obeter el valor real de la resta y se anexa el signo de negativo:\n",
    "\n",
    "\n",
    "$$\n",
    "Y-X=-comp_2(1110011)=-0001101=-13_{10}\n",
    "$$\n",
    "\n",
    "## Sumador-restador binario\n",
    "\n",
    "Usando el complemento a 2 se suprime la operación de la resta y solamente se hace necesario un complementador apropiado y un sumador completo como el que se desarrolló en las secciones anteriores. Cuando realizamos una resta complementamos a dos el substraendo, mientras que para sumar no es necesario. Estas operaciones se pueden lograr usando un complementador selectivo y un sumadorcompleto conectados para formar un sumador-restador.El complemento a 2 se aplica por ser el más habitual en los\n",
    "sistemas modernos. El complemento a 2 se puede obtener tomando el complemento a 1 y sumándole 1 al bit de menor peso. El complemento a 1 se implementa fácilmente a partir de circuitos inversores, mientras que la suma de 1 la conseguimos haciendo la entrada de acarreo del sumador paralelo igual a 1. De esta manera, usando el complemento a 1 y una entrada no usada\n",
    "del sumador, se obtiene de una forma muy sencilla el complemento a 2. En la resta en complemento a 2, como en el paso de corrección después de la suma, tenemos que complementar el resultado y añadirle un signo menos si se produce acarreo final. \n",
    "\n",
    "En la {numref}`sumador_restador` se muestra un circuito sumador-restador de 4 bits. La entrada $R$ controla el funcionamiento. Cuando $R=0$ el circuito funciona como un sumador, y cuando $R=1$ el circuito se convierte en un restador. Cada puerta OR exclusiva recibe la entrada $R$ y una de las entradas de $Y$, $Y_i$. Cuando $R=0$, tenemos $Y_i \\oplus 0$. Si los sumadores completos reciben el valor de $Y$, y la entrada de acarreo es 0, el circuito realiza $A+B$. Cuando $R=1$, tenemos $Y_i \\oplus 1=\\overline{Y}_i$ y $C_0=1$. En este caso, el circuito realiza la operación $A+comp_2(B)$.\n",
    "\n",
    "```{figure} /images/sumador_restador.png\n",
    ":height: 250px\n",
    ":width: 800px\n",
    ":name: sumador_restador\n",
    "Diagrama de un sumador-restador con acarreo serie.\n",
    "```\n",
    "La descripción en VHDL del sumador-restador incluye a la descripción del sumador completo. Es posible hacer uso de módulos ya analizados y simulados para conseguir la construcción o descripción de un sistema digital más complejo en el contexto de la lógica combinacional, los sistemas digitales complejos, incluso los microprocesadores de las computadoras son circitos lógicos combinacionales que incorporan infinidad de subsiseños. El código VHDL para el sumador-restador se muestra abajo:\n",
    "\n",
    "```VHDL\n",
    "--------------------------------------------------\n",
    "-- Código VHDL para la descripción de \n",
    "--    un sumador-restador con acarreo \n",
    "-- serie de dos números de 4 bits\n",
    "--------------------------------------------------\n",
    "library IEEE;\n",
    "use IEEE.STD_LOGIC_1164.ALL;\n",
    "------------------------------------------------  \n",
    "-- Se usa la descripción de un bloque \n",
    "--   básico el cual es un sumador completo\n",
    "--------------------------------------------------\n",
    "entity sumador_completo is\n",
    " Port (  X : in STD_LOGIC;\n",
    "         Y : in STD_LOGIC;\n",
    "         Z : in STD_LOGIC;\n",
    "         S : out STD_LOGIC;\n",
    "         Cout : out STD_LOGIC);\n",
    "end sumador_completo;\n",
    "-- Se describe la arquitectura a nivel compuerta \n",
    "architecture nivel_compuerta of sumador_completo is\n",
    " \n",
    "begin\n",
    " \n",
    " S <= X XOR Y XOR Z ;\n",
    " Cout <= (X AND Y) OR (Z AND X) OR (Z AND Y) ;\n",
    " \n",
    "end nivel_compuerta; \n",
    "----------------------------------------------------\n",
    "-- Se describe la entidad sumador-restador que \n",
    "-- contiene al sumador completo como bloque \n",
    "-- fundamental\n",
    "-------------------------------------------------\n",
    "library IEEE;\n",
    "use IEEE.STD_LOGIC_1164.ALL;\n",
    " \n",
    "entity sumador_restador is\n",
    "Port ( X : in STD_LOGIC_VECTOR (3 downto 0);\n",
    "       Y : in STD_LOGIC_VECTOR (3 downto 0);\n",
    "       R : in STD_LOGIC;\n",
    "       S : out STD_LOGIC_VECTOR (3 downto 0);\n",
    "       C4 : out STD_LOGIC);\n",
    "end sumador_restador;\n",
    " \n",
    "architecture arq of sumador_restador is\n",
    " \n",
    "-- Declaración del uso del sumador completo\n",
    "-- como bloque básico\n",
    "component sumador_completo\n",
    " Port (  X : in STD_LOGIC;\n",
    "         Y : in STD_LOGIC;\n",
    "         Z : in STD_LOGIC;\n",
    "         S : out STD_LOGIC;\n",
    "      Cout : out STD_LOGIC);\n",
    "end component;\n",
    " \n",
    "-- Acarreos intermedios\n",
    "signal c1,c2,c3: STD_LOGIC;\n",
    " \n",
    "begin\n",
    " \n",
    "-- Mapeo del sumador completo 4 veces, usado etiquetas \n",
    "-- Sc para interconectar los bloques\n",
    "Sc1: sumador_completo port map( X(0), R XOR Y(0), R, S(0), c1);\n",
    "Sc2: sumador_completo port map( X(1), R XOR Y(1), c1, S(1), c2);\n",
    "Sc3: sumador_completo port map( X(2), R XOR Y(2), c2, S(2), c3);\n",
    "Sc4: sumador_completo port map( X(3), R XOR Y(3), c3, S(3), C4);\n",
    " \n",
    "end arq;\n",
    "```\n",
    "El contenido del archivo test bench incluye al sumador-restador como un componente, el cual se utiliza para declarar al diseño bajo prueba (DUT), el código VHDL del test bench es el siguiente:\n",
    "\n",
    "```VHDL\n",
    "library IEEE;\n",
    "USE ieee.std_logic_1164.ALL;\n",
    " \n",
    "entity tb_sumador_restador is\n",
    "end tb_sumador_restador;\n",
    "\n",
    "architecture arq of tb_sumador_restador is\n",
    "-- Declaración del diseño bajo prueba DUT\n",
    "component sumador_restador\n",
    "Port ( X : in STD_LOGIC_VECTOR (3 downto 0);\n",
    "       Y : in STD_LOGIC_VECTOR (3 downto 0);\n",
    "       R : in STD_LOGIC;\n",
    "       S : out STD_LOGIC_VECTOR (3 downto 0);\n",
    "       C4 : out STD_LOGIC);\n",
    "end component;\n",
    "------------------------------------------------\n",
    "--   Entradas\n",
    "--  en este caso es importante usar valores por defecto\n",
    "--- para iniciar la simulación, usanod la asignación\n",
    "--- especial := (others => '0') para vectores de bits\n",
    "---- y := '0' para bits.\n",
    "-------------------------------------------------\n",
    "signal test_X : std_logic_vector(3 downto 0):= (others => '0');\n",
    "signal test_Y : std_logic_vector(3 downto 0):= (others => '0');\n",
    "signal test_R : std_logic:= '0';\n",
    "--Salidas\n",
    "signal test_S : std_logic_vector(3 downto 0);\n",
    "signal test_Cout : std_logic;\n",
    "begin\n",
    "-- Inicializacion del DUT\n",
    "DUT: sumador_restador\n",
    "Port map (\n",
    "X => test_X,\n",
    "Y => test_Y,\n",
    "R => test_R,\n",
    "S => test_S,\n",
    "C4 => test_Cout\n",
    ");\n",
    "-- Proceso de pruebas\n",
    "process\n",
    "begin\n",
    "-- Mantener reset inical por 100ns\n",
    "wait for 100 ns;\n",
    "-- Se prueban varias combinaciones de \n",
    "-- sumandos y sustraendos\n",
    "test_X <= \"1100\";\n",
    "test_Y <= \"0110\";\n",
    "wait for 100 ns;\n",
    "--- -------------------\n",
    "test_R <= '1';\n",
    "test_X <= \"1100\";\n",
    "test_Y <= \"0110\";\n",
    "wait for 100 ns;\n",
    "---------------------\n",
    "wait;\n",
    "end process;\n",
    "end;\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "formats": "md:myst",
   "text_representation": {
    "extension": ".md",
    "format_name": "myst",
    "format_version": 0.13,
    "jupytext_version": "1.11.5",
    "language": "es"
   }
  },
  "kernelspec": {
   "display_name": "Octave",
   "language": "Octave",
   "name": "Octave"
  },
  "source_map": [
   14
  ]
 },
 "nbformat": 4,
 "nbformat_minor": 5
}