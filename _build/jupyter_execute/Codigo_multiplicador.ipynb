{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "77849a2a",
   "metadata": {},
   "source": [
    "# Código VHDL para un multiplicador binario de cuatro bits.\n",
    "\n",
    "El código VHDL para un multiplicador binario de cuatro bits debe incluir como bloque fundamental a un sumador con acarreo serie como el que se muestra en la {numref}`multiplicador`.\n",
    "\n",
    "```VHDL\n",
    "--------------------------------------------------\n",
    "-- Código VHDL para la descripción de \n",
    "--    un sumador con acarreo \n",
    "-- serie de dos números de 4 bits\n",
    "--------------------------------------------------\n",
    "library IEEE;\n",
    "use IEEE.STD_LOGIC_1164.ALL;\n",
    "------------------------------------------------  \n",
    "-- Se usa la descripción de un bloque \n",
    "--   básico el cual es un sumador completo\n",
    "--------------------------------------------------\n",
    "entity sumador_completo is\n",
    " Port (  X : in STD_LOGIC;\n",
    "         Y : in STD_LOGIC;\n",
    "         Z : in STD_LOGIC;\n",
    "         S : out STD_LOGIC;\n",
    "         Cout : out STD_LOGIC);\n",
    "end sumador_completo;\n",
    "-- Se describe la arquitectura a nivel compuerta \n",
    "architecture nivel_compuerta of sumador_completo is\n",
    " \n",
    "begin\n",
    " \n",
    " S <= X XOR Y XOR Z ;\n",
    " Cout <= (X AND Y) OR (Z AND X) OR (Z AND Y) ;\n",
    " \n",
    "end nivel_compuerta; \n",
    "----------------------------------------------------\n",
    "-- Se describe la entidad sumador serie que \n",
    "-- contiene al sumador completo como bloque \n",
    "-- fundamental\n",
    "-------------------------------------------------\n",
    "library IEEE;\n",
    "use IEEE.STD_LOGIC_1164.ALL;\n",
    " \n",
    "entity sumador_serie is\n",
    "Port ( X : in STD_LOGIC_VECTOR (3 downto 0);\n",
    "       Y : in STD_LOGIC_VECTOR (3 downto 0);\n",
    "       C0 : in STD_LOGIC;\n",
    "       S : out STD_LOGIC_VECTOR (3 downto 0);\n",
    "       C4 : out STD_LOGIC);\n",
    "end sumador_serie;\n",
    " \n",
    "architecture arq_sumador_serie of sumador_serie is\n",
    " \n",
    "-- Declaración del uso del sumador completo\n",
    "-- como bloque básico\n",
    "component sumador_completo\n",
    " Port (  X : in STD_LOGIC;\n",
    "         Y : in STD_LOGIC;\n",
    "         Z : in STD_LOGIC;\n",
    "         S : out STD_LOGIC;\n",
    "      Cout : out STD_LOGIC);\n",
    "end component;\n",
    " \n",
    "-- Acarreos intermedios\n",
    "signal c1,c2,c3: STD_LOGIC;\n",
    " \n",
    "begin\n",
    " \n",
    "-- Mapeo del sumador completo 4 veces, usado etiquetas \n",
    "-- Sc para interconectar los bloques\n",
    "Sc1: sumador_completo port map( X(0), Y(0), C0, S(0), c1);\n",
    "Sc2: sumador_completo port map( X(1), Y(1), c1, S(1), c2);\n",
    "Sc3: sumador_completo port map( X(2), Y(2), c2, S(2), c3);\n",
    "Sc4: sumador_completo port map( X(3), Y(3), c3, S(3), C4);\n",
    " \n",
    "end arq_sumador_serie;\n",
    "----------------------------------------------------\n",
    "-- Se describe la entidad multiplicador que \n",
    "-- contiene al sumador_serie como bloque \n",
    "-- fundamental\n",
    "-------------------------------------------------\n",
    "library IEEE;\n",
    "use IEEE.STD_LOGIC_1164.ALL;\n",
    "\n",
    "entity multiplicador is\n",
    "     Port(\n",
    "         X: in std_logic_vector(3 downto 0);\n",
    "         Y: in std_logic_vector(3 downto 0);\n",
    "         P: out std_logic_vector(7 downto 0)     \n",
    "       );\n",
    "end multiplicador;\n",
    "\n",
    "architecture modular of multiplicador is\n",
    "\n",
    "   component sumador_serie \n",
    "        Port ( X : in STD_LOGIC_VECTOR (3 downto 0);\n",
    "               Y : in STD_LOGIC_VECTOR (3 downto 0);\n",
    "               C0 : in STD_LOGIC;\n",
    "               S : out STD_LOGIC_VECTOR (3 downto 0);\n",
    "               C4 : out STD_LOGIC);\n",
    "   end component;\n",
    "--- Productos de los terminos con operación lógica AND\n",
    "signal I0,I1,I2: std_logic_vector(3 downto 0);\n",
    "--- Las entradas B tienen tres bits de productos AND\n",
    "signal B0,B1,B2: std_logic_vector(3 downto 0);\n",
    "\n",
    "begin\n",
    "-- Y(1) a Y(3) productos AND\n",
    "I0 <= (X(3) AND Y(1),X(2) AND Y(1),X(1) AND Y(1),X(0) AND Y(1));\n",
    "I1 <= (X(3) AND Y(2),X(2) AND Y(2),X(1) AND Y(2),X(0) AND Y(2));\n",
    "I2 <= (X(3) AND Y(3),X(2) AND Y(3),X(1) AND Y(3),X(0) AND Y(3));\n",
    "-- Productos Y(0) y el producto Y(3) AND '0'\n",
    "B0 <= ('0', X(3) AND Y(0),X(2) AND Y(0),X(1) AND Y(0));\n",
    "\n",
    "-- Asciación modular\n",
    "moulo_1: \n",
    "    sumador_serie\n",
    "       Port map ( X => I0,\n",
    "                  Y => B0,\n",
    "                  C0 => '0',\n",
    "                   C4  => B1(3),\n",
    "                  S(3) => B1(2), -- La asociación se puede dar en cualquier orden\n",
    "                  S(2) => B1(1), -- Los elementos individuales de S estan asociados\n",
    "                  S(1) => B1(0), -- todos estos miembros se entregan continuamente\n",
    "                  S(0) => P(1)                    \n",
    "                    );\n",
    "\n",
    "moulo_2: \n",
    "    sumador_serie\n",
    "       Port map ( X => I1,\n",
    "                  Y => B1,\n",
    "                  C0 => '0',\n",
    "                   C4  => B2(3),\n",
    "                  S(3) => B2(2), -- La asociación se puede dar en cualquier orden\n",
    "                  S(2) => B2(1), -- Los elementos individuales de S estan asociados\n",
    "                  S(1) => B2(0), -- todos estos miembros se entregan continuamente\n",
    "                  S(0) => P(2)                    \n",
    "                    );\n",
    "moulo_3: \n",
    "    sumador_serie\n",
    "       Port map ( X => I2,\n",
    "                  Y => B2,\n",
    "                  C0 => '0',\n",
    "                   C4  => P(7),\n",
    "                    S => P(6 downto 3)-- Acoplando elementos por formalidad                    \n",
    "                    );\n",
    "\n",
    "  P(0) <= X(0) AND Y(0);\n",
    "\n",
    "end modular;\n",
    "````\n",
    "\n",
    "El test bench para probar el circuito multiplicador es el siguiente:\n",
    "\n",
    "```VHDL\n",
    "library IEEE;\n",
    "use IEEE.STD_LOGIC_1164.ALL;\n",
    "\n",
    "entity tb_multiplicador is\n",
    "end tb_multiplicador;\n",
    "\n",
    "architecture arq_tb of tb_multiplicador is \n",
    "\n",
    "\n",
    "component multiplicador\n",
    "     Port(\n",
    "         X: in std_logic_vector(3 downto 0);\n",
    "         Y: in std_logic_vector(3 downto 0);\n",
    "         P: out std_logic_vector(7 downto 0)     \n",
    "       );\n",
    "end component;\n",
    "\n",
    "\n",
    "signal test_X, test_Y: std_logic_vector(3 downto 0);\n",
    "signal test_P: std_logic_vector (7 downto 0);\n",
    "\n",
    "\n",
    "begin \n",
    "\n",
    "DUT: multiplicador\n",
    "     Port map(\n",
    "         X => test_X,\n",
    "         Y => test_Y,\n",
    "         P => test_P\n",
    "       );\n",
    "PRUEBAS:\n",
    " Process\n",
    "    begin\n",
    "        wait for 100 ns;\n",
    "        test_X <= \"0010\";\n",
    "        test_Y <= \"0110\"; \n",
    "         wait for 100 ns;\n",
    "        test_X <= \"1010\";\n",
    "        test_Y <= \"1110\"; \n",
    "         wait for 100 ns; \n",
    "        wait for 100 ns;\n",
    "        test_X <= \"1011\";\n",
    "        test_Y <= \"1010\"; \n",
    "    wait;  \n",
    " end process; \n",
    "end arq_tb;\n",
    "````\n",
    "\n",
    "\n",
    "Una opción diferente para realizar el test bench para el multiplicador de cuatro bits se puede estructurar de tal manera que se elaboren las operaciones en un formato que emula a una tabla de multiplicar.\n",
    "\n",
    "```VHDL\n",
    "library IEEE;\n",
    "use IEEE.STD_LOGIC_1164.ALL;\n",
    "\n",
    "entity tb_multiplicador is\n",
    "end tb_multiplicador;\n",
    "\n",
    "architecture arq_tb of tb_multiplicador is \n",
    "\n",
    "\n",
    "component multiplicador\n",
    "     Port(\n",
    "         X: in std_logic_vector(3 downto 0);\n",
    "         Y: in std_logic_vector(3 downto 0);\n",
    "         P: out std_logic_vector(7 downto 0)     \n",
    "       );\n",
    "end component;\n",
    "\n",
    "\n",
    "signal test_X, test_Y: std_logic_vector(3 downto 0);\n",
    "signal test_P: std_logic_vector (7 downto 0);\n",
    "use ieee.numeric_std.all;\n",
    "\n",
    "begin \n",
    "\n",
    "DUT: multiplicador\n",
    "     Port map(\n",
    "         X => test_X,\n",
    "         Y => test_Y,\n",
    "         P => test_P\n",
    "       );\n",
    "PRUEBAS:\n",
    " Process\n",
    "    begin\n",
    "       for i in 0 to 15 loop\n",
    "           test_X <= std_logic_vector(to_unsigned(i, test_X'length));\n",
    "            for j in 0 to 15 loop\n",
    "               test_Y <= std_logic_vector(to_unsigned(j, test_Y'length));\n",
    "               wait for 10 ns;          \n",
    "           end loop;\n",
    "       end loop;\n",
    "    wait;  \n",
    " end process; \n",
    "end arq_tb;\n",
    "````"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "formats": "md:myst",
   "text_representation": {
    "extension": ".md",
    "format_name": "myst",
    "format_version": 0.13,
    "jupytext_version": "1.11.5",
    "language": "es"
   }
  },
  "kernelspec": {
   "display_name": "Octave",
   "language": "Octave",
   "name": "Octave"
  },
  "source_map": [
   14
  ]
 },
 "nbformat": 4,
 "nbformat_minor": 5
}